material {
    "name" : "Chroma Key Video Material",
    "defines" : [
        "baseColor"
    ],
    "parameters" : [
        {
           "type" : "samplerExternal",
           "name" : "videoTexture"
        },
        {
            "type" : "float4",
            "name" : "keyColor"
        },
        {
            "type" : "bool",
            "name" : "enableChromaKey",
        },
        {
            "type" : "bool",
            "name" : "enableHalfMask"
        },
        {
            "type" : "float",
            "name" : "threshold"
        },
        {
            "type" : "float",
            "name" : "slope"
        }
    ],
    "requires" : [
        "position",
        "uv0",
        "color"
    ],
    "shadingModel" : "unlit",
    "blending" : "masked",
    "doubleSided" : true
}

fragment {
    vec3 desaturate(vec3 color, float amount) {
        vec3 gray = vec3(dot(vec3(0.2126, 0.7152, 0.0722), color));
        return vec3(mix(color, gray, amount));
    }

    void material(inout MaterialInputs material) {
        prepareMaterial(material);

        vec2 uv = getUV0();

        if (!gl_FrontFacing) {
          uv.x = 1.0 - uv.x;
        }

        vec4 color = texture(materialParams_videoTexture, uv).rgba;

        if (materialParams.enableChromaKey) {
            vec3 keyColor = materialParams.keyColor.rgb;

            float distance = abs(length(abs(keyColor - color.rgb)));
            float edge0 = materialParams.threshold * (1.0 - materialParams.slope);
            float alpha = smoothstep(edge0, materialParams.threshold, distance);
            color.rgb = desaturate(color.rgb, 1.0 - (alpha * alpha * alpha));

            material.baseColor.a = alpha;
            material.baseColor.rgb = inverseTonemapSRGB(color.rgb);
            material.baseColor.rgb *= material.baseColor.a;
        } else if (materialParams.enableHalfMask) {
            vec3 keyColor = materialParams.keyColor.rgb;
            float vtop = color.a;

            float3 mask_color = texture(materialParams_videoTexture, float2(uv.x, uv.y  + 0.25 * vtop - 0.25 * step(vtop, 0.0))).rgb;
            float distance = abs(length(abs(keyColor - mask_color.rgb)));
            float edge0 = materialParams.threshold * (1.0 - materialParams.slope);
            float alpha = smoothstep(edge0, materialParams.threshold, distance) * step(uv.y, 0.75) * step(0.25, uv.y);

            material.baseColor.rgba = float4(texture(materialParams_videoTexture, float2(uv.x, uv.y - 0.25 * vtop + 0.25 * step(vtop, 0.0))).rgb, alpha);
            material.baseColor.rgb = inverseTonemapSRGB(material.baseColor.rgb);
        } else {
            material.baseColor.rgb = inverseTonemapSRGB(color.rgb);
        }
    }
}